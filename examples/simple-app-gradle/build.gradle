buildscript {
    repositories {
        flatDir dirs: '../../build/libs'
		
        mavenLocal()
        mavenCentral()
	}
    dependencies {
        classpath 'com.dynatrace.diagnostics.automation:Dynatrace-Gradle-Plugin:7.0.0'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

/* java configuration */
apply plugin: 'java'

group = 'com.example.app'
version = '0.0.1'

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
/* compile and testing dependencies */
dependencies {
    testCompile 'junit:junit:4.12'
}

/* use dynatrace plugin */
apply plugin: 'dynatrace-gradle-plugin'

/* custom tasks that are depending on  Dynatrace Gradle Plugin tasks */
task PrintAgentInfo() << {
	print DtGetAgentInfo.agentName
}


task InjectAgent(dependsOn: DtStartTest) << {
	test.jvmArgs '-agentpath:/home/michal/dt/trunk-classic/jloadtrace/agent/lib64/libdtagent.so=name=Test_easyTravel,server=localhost,wait=5,optionTestRunIdJava=' + DtStartTest.testRunId
}

test.dependsOn InjectAgent
test.doLast {
	DtFinishTest {
		testRunId = DtStartTest.testRunId
	}
	tasks.DtFinishTest.execute()
	println "Dynatrace Gradle Plugin finish testRun ID="+DtStartTest.testRunId
}


task ConfigureMemoryDump(dependsOn: DtGetAgentInfo) << {
	DtMemoryDump.hostName = DtGetAgentInfo.agentHost
	DtMemoryDump.agentName = DtGetAgentInfo.agentName
	DtMemoryDump.processId = DtGetAgentInfo.agentProcessId
}

task ConfigureThreadDump(dependsOn: DtGetAgentInfo) << {
	DtThreadDump.hostName = DtGetAgentInfo.agentHost
	DtThreadDump.agentName = DtGetAgentInfo.agentName
	DtThreadDump.processId = DtGetAgentInfo.agentProcessId
}

/* Dynatrace Plugin tasks with configuration */
dynaTrace {
	serverUrl = 'https://localhost:8021'
	profile = 'easyTravel'
	ignoreSSLErrors = true
}

DtGetAgentInfo {
	infoForAgentByIndex = 0
	//infoForAgentByName = 'someName'
	
	finalizedBy PrintAgentInfo
}

DtMemoryDump {
	dumpType = 'memdump_simple'
	sessionLocked = false
	waitForDumpTimeout = 60000
	waitForDumpPollingInterval = 5000
	doGc = false
	autoPostProcess = false
	capturePrimitives = false
	captureStrings = false
	
	dependsOn ConfigureMemoryDump
}

DtReanalyzeSession {
	sessionName = 'session'
	reanalyzeSessionTimeout = 60000
	reanalyzeSessionPollingInterval = 5000
}

DtRestartCollector {
	restart = true /* true to restart, false to shutdown */
	collector = 'localhost' /* collector name */
}

DtRestartServer {
	restart = true /* true to restart, false to shutdown */
}

DtSensorPlacement {
	agentId = 123
}

DtStartRecording {
	sessionName = 'session'
	sessionDescription = 'some description'
	recordingOption = 'all'
	sessionLocked = false
	appendTimestamp = false
	//profile = 'someProfile'
}

DtStopRecording {
	doReanalyzeSession = false
	reanalyzeSessionTimeout = 60000
	reanalyzeSessionPollingInterval = 5000
	stopDelay = 0
	failOnError = true
	//profile = 'someProfile'
}

DtStartTest {
	versionBuild = 1
	versionMajor = 2
	versionMinor = 3
	versionRevision = 4
	versionMilestone = 5
	platform = 'Linux'
	marker = 'marker'
	category = 'unit'
	
    addCustomProperty 'testset', 'unit-tests'
}

DtStorePurePaths {
	recordingOption = 'all'
	sessionLocked = false
	appendTimestamp = false
	//profile = 'someProfile'
}

DtThreadDump {
	sessionLocked = false
	waitForDumpTimeout = 60000
	waitForDumpPollingInterval = 5000
	
	dependsOn ConfigureThreadDump
}

